{ Datei mit Definitionen fr Turtle-Graphic }

global "trt_course" set "trt_course" 0

global "trt_x"      set "trt_x"      ( scrn_width  / 2 )
global "trt_y"      set "trt_y"      ( scrn_height / 2 )
global "trt_mode"   set "trt_mode"   TRUE

define "_line" [ :x1 :y1 :x2 :y2 ] [
        
       if ( ( :x1 - :x2 ) or ( :y1 - :y2 ) ) [ 

          if ( abs ( :x1 - :x2 )  >=  abs ( :y1 - :y2 ) ) [

            if ( :x2 < :x1 ) [              { Koordinaten tauschen }
               local "swap"

               set "swap" :x1
               set "x1" :x2
               set "x2" :swap

               set "swap" :y1
               set "y1" :y2
               set "y2" :swap
            ]   

            local "steigung"
            set "steigung" ( ( :y2 - :y1 ) / ( :x2 - :x1 ) )
            local "x" set "x" 0
            while ( :x <= :x2 - :x1 ) [
             pset ( :x + :x1 ) ( :x * :steigung + :y1 )
             set "x" ( :x + 1 )
            ]

          ] else [

            if ( :y2 < :y1 ) [              { Koordinaten tauschen }
               local "swap"
   
               set "swap" :x1
               set "x1" :x2
               set "x2" :swap

               set "swap" :y1
               set "y1" :y2
               set "y2" :swap
            ]   

            local "steigung"
            set "steigung" ( ( :x2 - :x1 ) / ( :y2 - :y1 ) )
            local "y" set "y" 0
            while ( :y <= :y2 - :y1  ) [
             pset ( :y * :steigung + :x1 ) ( :y + :y1 )
             set "y" ( :y + 1 )
            ]
          ] 
       ]
]

define "aufxy" [ :x :y ] [
       if :trt_mode [
             _line :trt_x :trt_y :x :y
       ]
       set "trt_x" :x
       set "trt_y" :y
]

define "sh" NIL [
       set "trt_mode" FALSE
]

define "sa" NIL [
       set "trt_mode" TRUE
]

define "aufkurs" [ :a ] [
       set "trt_course" :a
       correct
]

define "bild" NIL [
       sh
       aufkurs 0
       aufxy ( scrn_width / 2 ) ( scrn_height / 2 )
       sa
       graph 
]

define "posx" NIL [
       returnv :trt_x ]

define "posy" NIL [
       returnv :trt_y ]

define "kurs" NIL [
       returnv :trt_course ]

define "li" [ :a ] [
       set "trt_course" ( :trt_course - :a )
       correct
]

define "re" [ :a ] [
       li  - :a ]

define "vw" [ :a ] [
       aufxy ( :trt_x + cos :trt_course * :a )
             ( :trt_y + sin :trt_course * :a )
]

define "rw" [ :a ] [
       vw  - :a ]

define "correct" NIL [
       while ( :trt_course < 0 ) [
             set "trt_course" ( :trt_course + 360 ) ]
       while ( :trt_course >= 360 ) [
             set "trt_course" ( :trt_course - 360 ) ]
       ]

println "Turtle - Befehle eingebaut!"
